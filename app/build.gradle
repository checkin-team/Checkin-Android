apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply from: '../versions.gradle'

def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.debug")))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "com.checkin.app.checkin"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        resValue "string", "build_time", buildTime()
        resValue "string", "build_host", hostName()
        resValue "string", "build_revision", revision()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "objectbox.myObjectBoxPackage": "com.checkin.app.checkin"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            versionNameSuffix ' - ' + revision()
        }
        staging {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            versionNameSuffix ' - ' + revision()
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    flavorDimensions "feature"
    productFlavors {
        extra {
            dimension "feature"
//            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
        core {
            dimension "feature"
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }
    packagingOptions {
        pickFirst 'META-INF/*'
//        pickFirst 'META-INF/proguard/coroutines.pro'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin libraries
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"

    //noinspection GradleDependency
    implementation "androidx.fragment:fragment-ktx:1.0.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"

    // AndroidX libraries
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'

    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    // FireBase libraries
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-auth:19.0.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    // Fabric Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    // Facebook SDK
    implementation 'com.facebook.android:facebook-android-sdk:4.37.0'
    implementation 'com.facebook.android:facebook-login:4.37.0'
    // QR Scanner
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    // Design Libraries
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.bumptech.glide:glide:' + versions.glide
    kapt 'com.github.bumptech.glide:compiler:' + versions.glide
    implementation 'jp.wasabeef:glide-transformations:3.3.0'
    implementation 'com.jakewharton:butterknife:' + versions.butterknife
    kapt 'com.jakewharton:butterknife-compiler:' + versions.butterknife
    implementation 'com.yarolegovich:discrete-scrollview:1.4.9'
    implementation 'com.appyvet:materialrangebar:1.4.4'
    implementation 'com.github.antonygolovin:fluentsnackbar:1.0.0'
    implementation 'com.joooonho:selectableroundedimageview:1.0.1'
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'
    implementation 'com.lyft:scissors:1.1.1'
    implementation 'com.borjabravo:readmoretextview:2.1.0'

    // Network libraries and JSON <-> POJO
    implementation(
            // Jackson
            [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson],
            [group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: versions.jackson],

            // Retrofit + okhttp3
            [group: 'com.squareup.retrofit2', name: 'retrofit', version: versions.retrofit],
            [group: 'com.squareup.retrofit2', name: 'converter-jackson', version: versions.retrofit]
    )

    // Include okhttp3 library of lower version because 3.13.x supports only API 21+
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:3.12.0"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.0'

    // ObjectBox components
    implementation 'io.objectbox:objectbox-android:' + versions.objectBox
    kapt 'io.objectbox:objectbox-processor:' + versions.objectBox
    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"

    // Dagger components
    implementation 'com.google.dagger:dagger:' + versions.dagger
    kapt 'com.google.dagger:dagger-compiler:' + versions.dagger

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.libraries.places:places:2.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Onboarding Screens
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.12.0'

    // Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // PayTM sdk
    implementation('com.paytm:pgplussdk:1.3.3') {
        transitive = true
    }

    // Shimmering Effects
    implementation 'com.facebook.shimmer:shimmer:0.4.0'

    // Modified libraries
    implementation project(':library:MaterialSearchView')
}

repositories {
    mavenCentral()
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

static def hostName() {
    return "${System.getProperty("user.name")}@${InetAddress.localHost.hostName}"
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString().trim()
}
