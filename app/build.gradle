apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply from: '../versions.gradle'

def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.debug")))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "com.checkin.app.checkin"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        resValue "string", "build_time", buildTime()
        resValue "string", "build_host", hostName()
        resValue "string", "build_revision", revision()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "objectbox.myObjectBoxPackage": "com.checkin.app.checkin"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            versionNameSuffix ' - ' + revision()
        }
        staging {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            versionNameSuffix ' - ' + revision()
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    flavorDimensions "feature"
    productFlavors {
        extra {
            dimension "feature"
//            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
        core {
            dimension "feature"
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation', 'ExtraTranslation'
    }
    packagingOptions {
        pickFirst 'META-INF/*'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Kotlin libraries
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"

    // Android KTX
    implementation(
            "androidx.core:core-ktx:$versions.ktx",
            "androidx.fragment:fragment-ktx:$versions.ktx"
    )

    // Lifecycle Android KTX
    implementation(
            "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle",
            "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle",
            "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
    )

    // AndroidX libraries
    implementation(
            "androidx.legacy:legacy-support-v4:1.0.0",
            "androidx.core:core:1.1.0",
            "androidx.constraintlayout:constraintlayout:1.1.3",
            "androidx.cardview:cardview:1.0.0",
            "androidx.multidex:multidex:2.0.1",
            "androidx.paging:paging-runtime:$versions.paging",
            "androidx.recyclerview:recyclerview:1.1.0",
            "androidx.viewpager2:viewpager2:1.0.0",
            "androidx.exifinterface:exifinterface:1.1.0",

            "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle",
            "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    )

    // Network libraries and JSON <-> POJO
    implementation(
            // Jackson
            [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson],
            [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson],
            [group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: versions.jackson],

            // Retrofit + okhttp3
            [group: 'com.squareup.retrofit2', name: 'retrofit', version: versions.retrofit],
            [group: 'com.squareup.retrofit2', name: 'converter-jackson', version: versions.retrofit]
    )


    // OkHttp3
    //noinspection GradleDependency
    implementation(
            "com.squareup.okhttp3:okhttp:$versions.okhttp",
            "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    )

    // Play Services
    implementation(
            "com.google.android.gms:play-services-auth:$versions.playService",
            // Google Maps
            "com.google.android.gms:play-services-maps:$versions.playService",
            "com.google.android.gms:play-services-location:$versions.playService"
    )

    // Google Places API
    implementation "com.google.android.libraries.places:places:2.1.0"
    // Google Material
    implementation "com.google.android.material:material:1.2.0-alpha04"

    // FireBase libraries
    implementation(
            "com.google.firebase:firebase-common-ktx:19.3.0",
            "com.google.firebase:firebase-auth:19.2.0",
            "com.google.firebase:firebase-messaging:20.1.0",
            "com.google.firebase:firebase-analytics:17.2.2",
            "com.google.firebase:firebase-config-ktx:19.1.0"
    )

    // Fabric Crashlytics
    implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"

    // Facebook SDK
    implementation(
            "com.facebook.android:facebook-android-sdk:$versions.facebook",
            "com.facebook.android:facebook-login:$versions.facebook"
    )

    // Glide
    implementation(
            "com.github.bumptech.glide:glide:$versions.glide",
            "jp.wasabeef:glide-transformations:4.1.0"
    )
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    // QR Scanner
    implementation "me.dm7.barcodescanner:zxing:1.9.13"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    kapt "com.jakewharton:butterknife-compiler:$versions.butterknife"

    // Epoxy
    implementation "com.airbnb.android:epoxy:$versions.epoxy"
    kapt "com.airbnb.android:epoxy-processor:$versions.epoxy"

    // Lottie
    implementation "com.airbnb.android:lottie:$versions.lottie"

    // ObjectBox components
    implementation "io.objectbox:objectbox-android:$versions.objectBox"
    kapt "io.objectbox:objectbox-processor:$versions.objectBox"

    // Koin
    implementation "org.koin:koin-androidx-scope:$versions.koin"
    implementation "org.koin:koin-androidx-viewmodel:$versions.koin"
    implementation "org.koin:koin-androidx-fragment:$versions.koin"

    // Design Libraries
    implementation "de.hdodenhof:circleimageview:2.2.0"
    implementation "com.yarolegovich:discrete-scrollview:1.4.9"
    implementation "com.appyvet:materialrangebar:1.4.4"
    implementation "com.github.antonygolovin:fluentsnackbar:1.0.0"
    implementation "com.github.adrielcafe:PageIndicatorView:1.0.6"// original library doesn't support ViewPager2
    implementation "com.lyft:scissors:1.1.1"
    implementation "com.borjabravo:readmoretextview:2.1.0"
    implementation "com.wdullaer:materialdatetimepicker:4.2.3"

    // Onboarding Screens
    implementation "com.getkeepsafe.taptargetview:taptargetview:1.12.0"

    // PayTM sdk
    implementation("com.paytm:pgplussdk:$versions.paytm") { transitive = true }

    // Shimmering Effects
    implementation "com.facebook.shimmer:shimmer:0.5.0"

    // Test libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha03'

    // Material Drawer
    implementation 'co.zsmb:materialdrawer-kt:3.0.0'

    // Modified libraries
    implementation project(':library:MaterialSearchView')
}

repositories {
    mavenCentral()
}

kapt {
    correctErrorTypes = true
    generateStubs = true
}

androidExtensions {
    experimental = true
}

static def buildTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

static def hostName() {
    return "${System.getProperty("user.name")}@${InetAddress.localHost.hostName}"
}

def revision() {
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = code
    }
    return code.toString().trim()
}
